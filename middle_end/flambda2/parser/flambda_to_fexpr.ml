open! Flambda.Import
open Flambda_to_fexpr_commons

let name env n =
  Name.pattern_match n
    ~var:(fun v : Fexpr.name -> Var (Env.find_var_exn env v))
    ~symbol:(fun s : Fexpr.name -> Symbol (Env.find_symbol_exn env s))

let float32 f = f |> Numeric_types.Float32_by_bit_pattern.to_float

let float f = f |> Numeric_types.Float_by_bit_pattern.to_float

let int i = i |> Target_ocaml_int.to_int64

let vec128 v = v |> Vector_types.Vec128.Bit_pattern.to_bits

let vec256 v = v |> Vector_types.Vec256.Bit_pattern.to_bits

let vec512 v = v |> Vector_types.Vec512.Bit_pattern.to_bits

let mask v = v |> Vector_types.Mask.Bit_pattern.to_bits

let targetint i = i |> Targetint_32_64.to_int64

let depth_or_infinity (d : int Or_infinity.t) : Fexpr.rec_info =
  match d with Finite d -> Depth d | Infinity -> Infinity

let rec rec_info env (ri : Rec_info_expr.t) : Fexpr.rec_info =
  match ri with
  | Const { depth; unrolling } -> (
    match unrolling with
    | Not_unrolling -> depth_or_infinity depth
    | Unrolling { remaining_depth } ->
      Unroll (remaining_depth, depth_or_infinity depth)
    | Do_not_unroll -> (
      match depth with
      | Infinity -> Do_not_inline
      | Finite _ ->
        Misc.fatal_errorf "unexpected finite depth with Do_not_unroll:@ %a"
          Rec_info_expr.print ri))
  | Var dv -> Var (Env.find_var_exn env dv)
  | Succ ri -> Succ (rec_info env ri)
  | Unroll_to (d, ri) -> Unroll (d, rec_info env ri)

let coercion env (co : Coercion.t) : Fexpr.coercion =
  match co with
  | Id -> Id
  | Change_depth { from; to_ } ->
    let from = rec_info env from in
    let to_ = rec_info env to_ in
    Change_depth { from; to_ }

let is_default_kind_with_subkind (k : Flambda_kind.With_subkind.t) =
  Flambda_kind.is_value (Flambda_kind.With_subkind.kind k)
  && not (Flambda_kind.With_subkind.has_useful_subkind_info k)

let rec subkind (k : Flambda_kind.With_subkind.Non_null_value_subkind.t) :
    Fexpr.subkind =
  match k with
  | Anything -> Anything
  | Boxed_float32 -> Boxed_float32
  | Boxed_float -> Boxed_float
  | Boxed_int32 -> Boxed_int32
  | Boxed_int64 -> Boxed_int64
  | Boxed_nativeint -> Boxed_nativeint
  | Boxed_vec128 -> Boxed_vec128
  | Boxed_vec256 -> Boxed_vec256
  | Boxed_vec512 -> Boxed_vec512
  | Boxed_mask -> Boxed_mask
  | Tagged_immediate -> Tagged_immediate
  | Variant { consts; non_consts } -> variant_subkind consts non_consts
  | Float_array -> Float_array
  | Immediate_array -> Immediate_array
  | Value_array -> Value_array
  | Generic_array -> Generic_array
  | Float_block { num_fields } -> Float_block { num_fields }
  | Unboxed_float32_array -> Unboxed_float32_array
  | Untagged_int_array -> Untagged_int_array
  | Untagged_int8_array -> Untagged_int8_array
  | Untagged_int16_array -> Untagged_int16_array
  | Unboxed_int32_array -> Unboxed_int32_array
  | Unboxed_int64_array -> Unboxed_int64_array
  | Unboxed_nativeint_array -> Unboxed_nativeint_array
  | Unboxed_vec128_array -> Unboxed_vec128_array
  | Unboxed_vec256_array -> Unboxed_vec256_array
  | Unboxed_vec512_array -> Unboxed_vec512_array
  | Unboxed_mask_array -> Unboxed_mask_array
  | Unboxed_product_array -> Unboxed_product_array

and variant_subkind consts non_consts : Fexpr.subkind =
  let consts =
    consts |> Target_ocaml_int.Set.elements
    |> List.map Target_ocaml_int.to_int64
  in
  let non_consts =
    non_consts |> Tag.Scannable.Map.bindings
    |> List.map (fun (tag, shape_and_fields) ->
        ( Tag.Scannable.to_int tag,
          match
            (shape_and_fields
              : Flambda_kind.With_subkind.Non_null_value_subkind
                .constructor_shape)
          with
          | Undetermined -> None
          | Determined (_shape, sk) -> Some (List.map kind_with_subkind sk) ))
  in
  Variant { consts; non_consts }

and kind_with_subkind (k : Flambda_kind.With_subkind.t) :
    Fexpr.kind_with_subkind =
  match Flambda_kind.With_subkind.kind k with
  | Value ->
    Value (subkind (Flambda_kind.With_subkind.non_null_value_subkind k))
  | Naked_number nnk -> Naked_number nnk
  | Region -> Region
  | Rec_info -> Rec_info

let kind_with_subkind_opt (k : Flambda_kind.With_subkind.t) :
    Fexpr.kind_with_subkind option =
  if is_default_kind_with_subkind k then None else Some (k |> kind_with_subkind)

let is_default_arity (a : [`Unarized] Flambda_arity.t) =
  match Flambda_arity.unarized_components a with
  | [k] -> is_default_kind_with_subkind k
  | _ -> false

let complex_arity (a : [`Complex] Flambda_arity.t) : Fexpr.arity =
  (* CR mshinwell: add unboxed arities to Fexpr *)
  Flambda_arity.unarize a |> List.map kind_with_subkind

let arity (a : [`Unarized] Flambda_arity.t) : Fexpr.arity =
  (* CR mshinwell: add unboxed arities to Fexpr *)
  Flambda_arity.unarized_components a |> List.map kind_with_subkind

let arity_opt (a : [`Unarized] Flambda_arity.t) : Fexpr.arity option =
  if is_default_arity a then None else Some (arity a)

let kinded_parameter env (kp : Bound_parameter.t) :
    Fexpr.kinded_parameter * Env.t =
  let k = Bound_parameter.kind kp |> kind_with_subkind_opt in
  let param, env = Env.bind_var env (Bound_parameter.var kp) in
  { param; kind = k }, env

let const c : Fexpr.const =
  match Reg_width_const.descr c with
  | Naked_immediate imm ->
    Naked_immediate
      (* CR mshinwell: machine_width should be passed through properly here *)
      (let machine_width = Target_system.Machine_width.Sixty_four in
       imm
       |> Target_ocaml_int.to_targetint machine_width
       |> Targetint_32_64.to_string)
  | Tagged_immediate imm ->
    Tagged_immediate
      (* CR mshinwell: machine_width should be passed through properly here *)
      (let machine_width = Target_system.Machine_width.Sixty_four in
       imm
       |> Target_ocaml_int.to_targetint machine_width
       |> Targetint_32_64.to_string)
  | Naked_float f -> Naked_float (f |> float)
  | Naked_float32 f -> Naked_float32 (f |> float32)
  | Naked_int8 i -> Naked_int8 i
  | Naked_int16 i -> Naked_int16 i
  | Naked_int32 i -> Naked_int32 i
  | Naked_int64 i -> Naked_int64 i
  | Naked_vec128 bits ->
    Naked_vec128 (Vector_types.Vec128.Bit_pattern.to_bits bits)
  | Naked_vec256 bits ->
    Naked_vec256 (Vector_types.Vec256.Bit_pattern.to_bits bits)
  | Naked_vec512 bits ->
    Naked_vec512 (Vector_types.Vec512.Bit_pattern.to_bits bits)
  | Naked_mask bits -> Naked_mask (Vector_types.Mask.Bit_pattern.to_bits bits)
  | Naked_nativeint i -> Naked_nativeint (i |> targetint)
  | Null -> Null
  | Poison (kind, name) ->
    let kind = kind_with_subkind (Flambda_kind.With_subkind.anything kind) in
    Poison (kind, name)

let simple env s =
  Simple.pattern_match s
    ~name:(fun n ~coercion:co : Fexpr.simple ->
      let s : Fexpr.simple =
        match name env n with Var v -> Var v | Symbol s -> Symbol s
      in
      if Coercion.is_id co
      then s
      else
        let co = coercion env co in
        Coerce (s, co))
    ~const:(fun c -> Fexpr.Const (const c))

let recursive_flag (r : Recursive.t) : Fexpr.is_recursive =
  match r with Recursive -> Recursive | Non_recursive -> Nonrecursive

let alloc_mode_for_allocations env (alloc : Alloc_mode.For_allocations.t) :
    Fexpr.alloc_mode_for_allocations =
  match alloc with
  | Heap { alloc_region } ->
    let alloc_region = Env.find_region_exn env alloc_region in
    Heap { alloc_region }
  | Local { alloc_region; region } ->
    let alloc_region = Env.find_region_exn env alloc_region in
    let region = Env.find_region_exn env region in
    Local { alloc_region; region }

let alloc_mode_for_applications env (alloc : Alloc_mode.For_applications.t) :
    Fexpr.region Fexpr.alloc_mode_for_applications =
  match alloc with
  | Not_alloc_stack { alloc_region } ->
    let alloc_region = Env.find_region_exn env alloc_region in
    Not_alloc_stack { alloc_region }
  | Maybe_alloc_stack { alloc_region; region; ghost_region } ->
    let alloc_region = Env.find_region_exn env alloc_region in
    let region = Env.find_region_exn env region in
    let ghost_region = Env.find_region_exn env ghost_region in
    Maybe_alloc_stack { alloc_region; region; ghost_region }

let prim env (p : Flambda_primitive.t) : Fexpr.prim =
  let p, args = Fexpr_prim.OfFlambda.prim env p in
  p, List.map (simple env) args

let value_slots env map =
  List.map
    (fun (var, value) ->
      let kind : Flambda_kind.Naked_number_kind.t option =
        match Value_slot.kind var with
        | Value -> None
        | Naked_number naked_number_kind -> Some naked_number_kind
        | (Region | Rec_info) as kind ->
          Misc.fatal_errorf "Value slot %a of unexpected kind %a" Simple.print
            value Flambda_kind.print kind
      in
      let var = Env.translate_value_slot env var in
      let value = simple env value in
      { Fexpr.var; value; kind })
    (map |> Value_slot.Map.bindings)

let function_declaration env code_id function_slot alloc : Fexpr.fun_decl =
  let code_id = Env.find_code_id_exn env code_id in
  let function_slot = Env.translate_function_slot env function_slot in
  (* Omit the function slot when possible *)
  let function_slot =
    if String.equal code_id.txt function_slot.txt
    then None
    else Some function_slot
  in
  { code_id; function_slot; alloc }

let set_of_closures env sc alloc =
  let fun_decls =
    List.map
      (fun (function_slot, fun_decl) ->
        function_declaration env fun_decl function_slot alloc)
      (Set_of_closures.function_decls sc
      |> Function_declarations.funs_in_order
      |> Function_slot.Lmap.map (function
        | Function_declarations.Deleted _ -> Misc.fatal_error "todo"
        | Function_declarations.Code_id { code_id; only_full_applications = _ }
          ->
          code_id)
      |> Function_slot.Lmap.bindings)
  in
  let elts = value_slots env (Set_of_closures.value_slots sc) in
  let elts = match elts with [] -> None | _ -> Some elts in
  fun_decls, elts

let field_of_block env field =
  Simple.pattern_match'
    (Simple.With_debuginfo.simple field)
    ~var:(fun var ~coercion:_ : Fexpr.field_of_block ->
      Dynamically_computed (Env.find_var_exn env var))
    ~symbol:(fun symbol ~coercion:_ : Fexpr.field_of_block ->
      Symbol (Env.find_symbol_exn env symbol))
    ~const:(fun cst : Fexpr.field_of_block -> Const (const cst))

let or_variable f env (ov : _ Or_variable.t) : _ Fexpr.or_variable =
  match ov with
  | Const c -> Const (f c)
  | Var (v, _dbg) -> Var (Env.find_var_exn env v)

let static_const env (sc : Static_const.t) : Fexpr.static_data =
  match sc with
  | Block (tag, mutability, _shape, fields) ->
    let tag = tag |> Tag.Scannable.to_int in
    let elements = List.map (field_of_block env) fields in
    Block { tag; mutability; elements }
  | Set_of_closures _ -> assert false
  | Boxed_float32 f -> Boxed_float32 (or_variable float32 env f)
  | Boxed_float f -> Boxed_float (or_variable float env f)
  | Boxed_int32 i -> Boxed_int32 (or_variable Fun.id env i)
  | Boxed_int64 i -> Boxed_int64 (or_variable Fun.id env i)
  | Boxed_nativeint i -> Boxed_nativeint (or_variable targetint env i)
  | Boxed_vec128 i -> Boxed_vec128 (or_variable vec128 env i)
  | Boxed_vec256 i -> Boxed_vec256 (or_variable vec256 env i)
  | Boxed_vec512 i -> Boxed_vec512 (or_variable vec512 env i)
  | Boxed_mask i -> Boxed_mask (or_variable mask env i)
  | Immutable_float_block elements ->
    Immutable_float_block (List.map (or_variable float env) elements)
  | Immutable_float_array elements ->
    Immutable_float_array (List.map (or_variable float env) elements)
  | Immutable_float32_array elements ->
    Immutable_float32_array (List.map (or_variable float32 env) elements)
  | Immutable_value_array elements ->
    Immutable_value_array (List.map (field_of_block env) elements)
  | Immutable_int_array elements ->
    Immutable_int_array (List.map (or_variable int env) elements)
  | Immutable_int8_array elements ->
    Immutable_int8_array (List.map (or_variable Fun.id env) elements)
  | Immutable_int16_array elements ->
    Immutable_int16_array (List.map (or_variable Fun.id env) elements)
  | Immutable_int32_array elements ->
    Immutable_int32_array (List.map (or_variable Fun.id env) elements)
  | Immutable_int64_array elements ->
    Immutable_int64_array (List.map (or_variable Fun.id env) elements)
  | Immutable_nativeint_array elements ->
    Immutable_nativeint_array (List.map (or_variable targetint env) elements)
  | Immutable_vec128_array elements ->
    Immutable_vec128_array (List.map (or_variable vec128 env) elements)
  | Immutable_vec256_array elements ->
    Immutable_vec256_array (List.map (or_variable vec256 env) elements)
  | Immutable_vec512_array elements ->
    Immutable_vec512_array (List.map (or_variable vec512 env) elements)
  | Immutable_mask_array elements ->
    Immutable_mask_array (List.map (or_variable mask env) elements)
  | Empty_array array_kind -> Empty_array array_kind
  | Immutable_string s -> Immutable_string s

let inlining_state (is : Inlining_state.t) : Fexpr.inlining_state option =
  if Inlining_state.equal is (Inlining_state.default ~round:0)
  then None
  else
    let depth = Inlining_state.depth is in
    (* TODO: inlining arguments *)
    Some { depth }

let rec expr env e =
  match Flambda.Expr.descr e with
  | Let l -> let_expr env l
  | Let_cont lc -> let_cont_expr env lc
  | Apply app -> apply_expr env app
  | Apply_cont app_cont -> apply_cont_expr env app_cont
  | Switch switch -> switch_expr env switch
  | Invalid { message } -> invalid_expr env ~message

and let_expr env le =
  Flambda.Let_expr.pattern_match le ~f:(fun bound ~body : Fexpr.expr ->
      let defining_expr = Flambda.Let_expr.defining_expr le in
      match bound with
      | Singleton var -> dynamic_let_expr env [var] defining_expr body
      | Set_of_closures value_slots ->
        dynamic_let_expr env value_slots defining_expr body
      | Static bound_static ->
        static_let_expr env bound_static defining_expr body)

and dynamic_let_expr env vars (defining_expr : Flambda.Named.t) body :
    Fexpr.expr =
  let vars, body_env = map_accum_left Env.bind_bound_var env vars in
  let body = expr body_env body in
  let defining_exprs, value_slots =
    match defining_expr with
    | Simple s -> ([Simple (simple env s)] : Fexpr.named list), None
    | Prim (p, _dbg) -> ([Prim (prim env p)] : Fexpr.named list), None
    | Set_of_closures (sc, alloc_mode) ->
      let alloc_mode = alloc_mode_for_allocations env alloc_mode in
      let fun_decls, value_slots = set_of_closures env sc alloc_mode in
      let defining_exprs =
        List.map (fun decl : Fexpr.named -> Fexpr.Closure decl) fun_decls
      in
      defining_exprs, value_slots
    | Rec_info ri -> ([Rec_info (rec_info env ri)] : Fexpr.named list), None
    | Static_consts _ -> assert false
  in
  if List.compare_lengths vars defining_exprs <> 0
  then Misc.fatal_error "Mismatched vars vs. values";
  let bindings =
    List.map2
      (fun var defining_expr -> { Fexpr.var; defining_expr })
      vars defining_exprs
  in
  Let { bindings; value_slots; body }

and static_let_expr env bound_static defining_expr body : Fexpr.expr =
  let static_consts =
    Named.must_be_static_consts defining_expr |> Static_const_group.to_list
  in
  let bound_static = bound_static |> Bound_static.to_list in
  let env =
    let bind_names env (pat : Bound_static.Pattern.t) =
      match pat with
      | Code _code_id ->
        (* Already bound at the beginning; see [bind_all_code_ids] *)
        env
      | Block_like symbol ->
        let _, env = Env.bind_symbol env symbol in
        env
      | Set_of_closures closure_symbols ->
        Function_slot.Lmap.fold
          (fun _function_slot symbol env ->
            let _, env = Env.bind_symbol env symbol in
            env)
          closure_symbols env
    in
    List.fold_left bind_names env bound_static
  in
  let translate_const (pat : Bound_static.Pattern.t)
      (const : Static_const_or_code.t) : Fexpr.symbol_binding =
    match pat, const with
    | Block_like symbol, Static_const const ->
      (* This is a binding occurrence, but it should have been added
       * already during the first pass *)
      let symbol = Env.find_symbol_exn env symbol in
      let defining_expr = static_const env const in
      Data { symbol; defining_expr }
    | Set_of_closures closure_symbols, Static_const const ->
      let set = Static_const.must_be_set_of_closures const in
      let fun_decls, elements =
        set_of_closures env set (Heap { alloc_region = Toplevel_alloc_region })
      in
      let symbols_by_function_slot =
        closure_symbols |> Function_slot.Lmap.bindings
        |> Function_slot.Map.of_list
      in
      let function_slots =
        Set_of_closures.function_decls set
        |> Function_declarations.funs_in_order |> Function_slot.Lmap.keys
      in
      let bindings =
        List.map2
          (fun fun_decl function_slot : Fexpr.static_closure_binding ->
            let symbol =
              Function_slot.Map.find function_slot symbols_by_function_slot
            in
            let symbol = Env.find_symbol_exn env symbol in
            { symbol; fun_decl })
          fun_decls function_slots
      in
      Set_of_closures { bindings; elements }
    | Code code_id, Code code ->
      let code_id = Env.find_code_id_exn env code_id in
      let newer_version_of =
        Option.map (Env.find_code_id_exn env) (Code.newer_version_of code)
      in
      let param_arity = Some (complex_arity (Code.params_arity code)) in
      let ret_arity = Code.result_arity code |> arity_opt in
      let recursive = recursive_flag (Code.recursive code) in
      let inline =
        if Flambda2_terms.Inline_attribute.is_default (Code.inline code)
        then None
        else Some (Code.inline code)
      in
      let loopify =
        if
          Flambda2_terms.Loopify_attribute.equal (Code.loopify code)
            Default_loopify_and_not_tailrec
        then None
        else Some (Code.loopify code)
      in
      let is_tupled = Code.is_tupled code in
      let stub = Code.stub code in
      let params_and_body =
        Flambda.Function_params_and_body.pattern_match
          (Code.params_and_body code)
          ~f:(fun
              ~return_continuation
              ~exn_continuation
              params
              ~body
              ~my_closure
              ~is_my_closure_used:_
              ~my_alloc_mode
              ~my_depth
              ~free_names_of_body:_
              :
              Fexpr.params_and_body
            ->
            let ret_cont, env =
              Env.bind_named_continuation env return_continuation
            in
            let exn_cont, env =
              Env.bind_named_continuation env exn_continuation
            in
            let params, env =
              map_accum_left kinded_parameter env
                (Bound_parameters.to_list params)
            in
            let closure_var, env = Env.bind_var env my_closure in
            let (region_vars : _ Fexpr.alloc_mode_for_applications), env =
              match my_alloc_mode with
              | Not_alloc_stack { alloc_region } ->
                let alloc_region, env = Env.bind_var env alloc_region in
                Not_alloc_stack { alloc_region }, env
              | Maybe_alloc_stack
                  { alloc_region = my_alloc_region;
                    region = my_region;
                    ghost_region = my_ghost_region
                  } ->
                let alloc_region, env = Env.bind_var env my_alloc_region in
                let region, env = Env.bind_var env my_region in
                let ghost_region, env = Env.bind_var env my_ghost_region in
                Maybe_alloc_stack { alloc_region; region; ghost_region }, env
            in
            let depth_var, env = Env.bind_var env my_depth in
            let body = expr env body in
            (* CR-someday lmaurer: Omit exn_cont, closure_var if not used *)
            { params;
              ret_cont;
              exn_cont;
              closure_var;
              region_vars;
              depth_var;
              body
            })
      in
      let code_size =
        Code.cost_metrics code |> Cost_metrics.size |> Code_size.to_int
      in
      let result_mode : Fexpr.alloc_mode_for_return =
        match Code.result_mode code with
        | Not_alloc_stack -> Not_alloc_stack
        | Maybe_alloc_stack -> Maybe_alloc_stack
      in
      Code
        { id = code_id;
          newer_version_of;
          param_arity;
          ret_arity;
          recursive;
          inline;
          loopify;
          params_and_body;
          code_size;
          is_tupled;
          stub;
          result_mode
        }
    | Code code_id, Deleted_code ->
      Deleted_code (code_id |> Env.find_code_id_exn env)
    | (Code _ | Block_like _), _ | Set_of_closures _, (Code _ | Deleted_code) ->
      Misc.fatal_errorf "Mismatched pattern and constant: %a vs. %a"
        Bound_static.Pattern.print pat Static_const_or_code.print const
  in
  let bindings = List.map2 translate_const bound_static static_consts in
  let body = expr env body in
  (* If there's exactly one set of closures, make it implicit *)
  let only_set_of_closures =
    let rec loop only_set (bindings : Fexpr.symbol_binding list) =
      match bindings with
      | [] -> only_set
      | Set_of_closures set :: bindings -> (
        match only_set with None -> loop (Some set) bindings | Some _ -> None)
      | (Data _ | Code _ | Deleted_code _ | Closure _) :: bindings ->
        loop only_set bindings
    in
    loop None bindings
  in
  match only_set_of_closures with
  | None -> Let_symbol { bindings; value_slots = None; body }
  | Some { bindings = _; elements = value_slots } ->
    let bindings =
      List.concat_map
        (fun (binding : Fexpr.symbol_binding) ->
          match binding with
          | Set_of_closures { bindings; elements = _ } ->
            List.map (fun closure -> Fexpr.Closure closure) bindings
          | Data _ | Code _ | Deleted_code _ | Closure _ -> [binding])
        bindings
    in
    Let_symbol { bindings; value_slots; body }

and let_cont_expr env (lc : Flambda.Let_cont_expr.t) =
  match lc with
  | Non_recursive { handler; _ } ->
    Flambda.Non_recursive_let_cont_handler.pattern_match handler
      ~f:(fun c ~body ->
        let sort = Continuation.sort c in
        let c, body_env = Env.bind_named_continuation env c in
        let binding =
          cont_handler env c sort
            (Flambda.Non_recursive_let_cont_handler.handler handler)
        in
        let body = expr body_env body in
        Fexpr.Let_cont { recursive = Nonrecursive; bindings = [binding]; body })
  | Recursive handlers ->
    Flambda.Recursive_let_cont_handlers.pattern_match handlers
      ~f:(fun ~invariant_params ~body handlers ->
        let params, env =
          map_accum_left kinded_parameter env
            (Bound_parameters.to_list invariant_params)
        in
        let env =
          List.fold_right
            (fun c env ->
              let _, env = Env.bind_named_continuation env c in
              env)
            (Flambda.Continuation_handlers.domain handlers)
            env
        in
        let bindings =
          List.map
            (fun (c, handler) ->
              let sort = Continuation.sort c in
              let c =
                match Env.find_continuation_exn env c with
                | Named c -> c
                | Special _ -> assert false
              in
              cont_handler env c sort handler)
            (handlers |> Flambda.Continuation_handlers.to_map
           |> Continuation.Lmap.bindings)
        in
        let body = expr env body in
        Fexpr.Let_cont { recursive = Recursive params; bindings; body })

and cont_handler env cont_id (sort : Continuation.Sort.t) h =
  let is_exn_handler = Flambda.Continuation_handler.is_exn_handler h in
  let sort : Fexpr.continuation_sort option =
    match sort with
    | Normal_or_exn -> if is_exn_handler then Some Exn else None
    | Define_root_symbol ->
      assert (not is_exn_handler);
      Some Define_root_symbol
    | Return | Toplevel_return -> assert false
  in
  Flambda.Continuation_handler.pattern_match h
    ~f:(fun params ~handler : Fexpr.continuation_binding ->
      let params, env =
        map_accum_left kinded_parameter env (Bound_parameters.to_list params)
      in
      let handler = expr env handler in
      { name = cont_id; params; sort; handler })

and apply_expr env (app : Apply_expr.t) : Fexpr.expr =
  let func = Option.map (simple env) (Apply_expr.callee app) in
  let continuation : Fexpr.result_continuation =
    match Apply_expr.continuation app with
    | Return c -> Return (Env.find_continuation_exn env c)
    | Never_returns -> Never_returns
  in
  let exn_continuation =
    let ec = Apply_expr.exn_continuation app in
    let c = Exn_continuation.exn_handler ec in
    let ea =
      List.map
        (fun (s, k) -> simple env s, kind_with_subkind k)
        (Exn_continuation.extra_args ec)
    in
    Env.find_continuation_exn env c, ea
  in
  let args = List.map (simple env) (Apply_expr.args app) in
  let alloc_mode =
    alloc_mode_for_applications env (Apply_expr.return_mode app)
  in
  let call_kind : Fexpr.call_kind =
    match Apply_expr.call_kind app with
    | Function { function_call = Direct code_id } ->
      let code_id = Env.find_code_id_exn env code_id in
      let function_slot = None in
      (* CR mshinwell: remove [function_slot] *)
      Function (Direct { code_id; function_slot })
    | Function
        { function_call = Indirect_unknown_arity | Indirect_known_arity _ } ->
      Function Indirect
    | C_call { needs_caml_c_call; _ } -> C_call { alloc = needs_caml_c_call }
    | Method { kind; obj } -> Method { kind; obj = simple env obj }
    | Effect _ -> Misc.fatal_error "TODO: Effect call kind"
  in
  let param_arity = Apply_expr.args_arity app in
  let return_arity = Apply_expr.return_arity app in
  let arities : Fexpr.function_arities option =
    match Apply_expr.call_kind app with
    | Function { function_call = Indirect_known_arity _ } ->
      let params_arity = Some (complex_arity param_arity) in
      let ret_arity = arity return_arity in
      Some { params_arity; ret_arity }
    | Function { function_call = Direct _ } ->
      if is_default_arity return_arity
      then None
      else
        let params_arity =
          (* Parameter arity is never specified for a direct call *)
          None
        in
        let ret_arity = arity return_arity in
        Some { params_arity; ret_arity }
    | C_call _ ->
      let params_arity = Some (complex_arity param_arity) in
      let ret_arity = arity return_arity in
      Some { params_arity; ret_arity }
    | Function { function_call = Indirect_unknown_arity } -> None
    | Method _ ->
      (* CR keryan: maybe use the method kind *)
      None
    | Effect _ -> assert false
  in
  let inlined : Fexpr.inlined_attribute option =
    if Flambda2_terms.Inlined_attribute.is_default (Apply_expr.inlined app)
    then None
    else
      match Apply_expr.inlined app with
      | Default_inlined -> Some Default_inlined
      | Hint_inlined -> Some Hint_inlined
      | Forward_inlined -> Some Forward_inlined
      | Always_inlined _ -> Some Always_inlined
      | Unroll (n, _) -> Some (Unroll n)
      | Never_inlined -> Some Never_inlined
  in
  let inlining_state = inlining_state (Apply_expr.inlining_state app) in
  Apply
    { func;
      continuation;
      exn_continuation;
      args;
      call_kind;
      alloc_mode;
      inlined;
      inlining_state;
      arities
    }

and apply_cont_expr env app_cont : Fexpr.expr =
  Apply_cont (apply_cont env app_cont)

and apply_cont env app_cont : Fexpr.apply_cont =
  let cont =
    Env.find_continuation_exn env (Apply_cont_expr.continuation app_cont)
  in
  let trap_action =
    Apply_cont_expr.trap_action app_cont
    |> Option.map (fun (action : Trap_action.t) : Fexpr.trap_action ->
        match action with
        | Push { exn_handler } ->
          let exn_handler = Env.find_continuation_exn env exn_handler in
          Push { exn_handler }
        | Pop { exn_handler; raise_kind } ->
          let exn_handler = Env.find_continuation_exn env exn_handler in
          Pop { exn_handler; raise_kind })
  in
  let args = List.map (simple env) (Apply_cont_expr.args app_cont) in
  { cont; trap_action; args }

and switch_expr env switch : Fexpr.expr =
  let scrutinee = simple env (Switch_expr.scrutinee switch) in
  let cases =
    List.map
      (fun (imm, arm) ->
        let app_cont = Switch_expr.arm_action arm in
        let tag =
          (* TODO: machine_width should be passed through properly here *)
          let machine_width = Target_system.Machine_width.Sixty_four in
          imm
          |> Target_ocaml_int.to_targetint machine_width
          |> Targetint_32_64.to_int
        in
        let app_cont = apply_cont env app_cont in
        tag, Fexpr.Named_cont app_cont)
      (Switch_expr.arms switch |> Target_ocaml_int.Map.bindings)
  in
  Switch { scrutinee; cases }

and invalid_expr _env ~message : Fexpr.expr = Invalid { message }

(* Iter on all sets of closures of a given program. *)
module Iter = struct
  let rec expr f_c f_s e =
    match (Expr.descr e : Expr.descr) with
    | Let e' -> let_expr f_c f_s e'
    | Let_cont e' -> let_cont f_c f_s e'
    | Apply e' -> apply_expr f_c f_s e'
    | Apply_cont e' -> apply_cont f_c f_s e'
    | Switch e' -> switch f_c f_s e'
    | Invalid { message = _ } -> ()

  and named let_expr (bound_pattern : Bound_pattern.t) f_c f_s n =
    match (n : Named.t) with
    | Simple _ | Prim _ | Rec_info _ -> ()
    | Set_of_closures (s, _alloc_mode) ->
      let is_phantom =
        Name_mode.is_phantom (Bound_pattern.name_mode bound_pattern)
      in
      f_s ~closure_symbols:None ~is_phantom s
    | Static_consts consts -> (
      match bound_pattern with
      | Static bound_static -> static_consts f_c f_s bound_static consts
      | Singleton _ | Set_of_closures _ ->
        Misc.fatal_errorf
          "[Static_const] can only be bound to a [Static] pattern:@ %a"
          Let.print let_expr)

  and let_expr f_c f_s t =
    Let.pattern_match t ~f:(fun bound_pattern ~body ->
        let e = Let.defining_expr t in
        named t bound_pattern f_c f_s e;
        expr f_c f_s body)

  and let_cont f_c f_s (let_cont : Flambda.Let_cont.t) =
    match let_cont with
    | Non_recursive { handler; _ } ->
      Non_recursive_let_cont_handler.pattern_match handler ~f:(fun k ~body ->
          let h = Non_recursive_let_cont_handler.handler handler in
          let_cont_aux f_c f_s k h body)
    | Recursive handlers ->
      Recursive_let_cont_handlers.pattern_match handlers
        ~f:(fun ~invariant_params:_ ~body conts ->
          assert (not (Continuation_handlers.contains_exn_handler conts));
          let_cont_rec f_c f_s conts body)

  and let_cont_aux f_c f_s k h body =
    continuation_handler f_c f_s k h;
    expr f_c f_s body

  and let_cont_rec f_c f_s conts body =
    let map = Continuation_handlers.to_map conts in
    Continuation.Lmap.iter (continuation_handler f_c f_s) map;
    expr f_c f_s body

  and continuation_handler f_c f_s _ h =
    Continuation_handler.pattern_match h ~f:(fun _ ~handler ->
        expr f_c f_s handler)

  (* Expression application, continuation application and Switches only use
     single expressions and continuations, so no sets_of_closures can
     syntatically appear inside. *)
  and apply_expr _ _ _ = ()

  and apply_cont _ _ _ = ()

  and switch _ _ _ = ()

  and static_consts f_c f_s bound_static static_consts =
    Static_const_group.match_against_bound_static static_consts bound_static
      ~init:()
      ~code:(fun () code_id (code : Code.t) ->
        f_c ~id:code_id (Some code);
        let params_and_body = Code.params_and_body code in
        Function_params_and_body.pattern_match params_and_body
          ~f:(fun
              ~return_continuation:_
              ~exn_continuation:_
              _
              ~body
              ~my_closure:_
              ~is_my_closure_used:_
              ~my_alloc_mode:_
              ~my_depth:_
              ~free_names_of_body:_
            -> expr f_c f_s body))
      ~deleted_code:(fun () code_id -> f_c ~id:code_id None)
      ~set_of_closures:(fun () ~closure_symbols set_of_closures ->
        f_s ~closure_symbols:(Some closure_symbols) ~is_phantom:false
          set_of_closures)
      ~block_like:(fun () _ _ -> ())
end

let ignore_code ~id:_ _ = ()

let ignore_set_of_closures ~closure_symbols:_ ~is_phantom:_ _ = ()

let iter ?(code = ignore_code) ?(set_of_closures = ignore_set_of_closures) unit
    =
  Iter.expr code set_of_closures (Flambda_unit.body unit)

let bind_all_code_ids env unit =
  let env = ref env in
  iter unit ~code:(fun ~id _code ->
      let _id, new_env = Env.bind_code_id !env id in
      env := new_env);
  !env

let conv flambda_unit =
  let done_ = Flambda_unit.return_continuation flambda_unit in
  let error = Flambda_unit.exn_continuation flambda_unit in
  let env = Env.create () in
  let env = Env.bind_special_continuation env done_ ~to_:Done in
  let env = Env.bind_special_continuation env error ~to_:Error in
  let env =
    Env.bind_toplevel_alloc_region env
      (Flambda_unit.toplevel_my_alloc_region flambda_unit)
  in
  (* Bind all code ids in toplevel let bindings at the start, since they don't
     necessarily occur in dependency order *)
  let env = bind_all_code_ids env flambda_unit in
  let body = expr env (Flambda_unit.body flambda_unit) in
  { Fexpr.body }
